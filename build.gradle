plugins {
    id 'base'
    id 'net.researchgate.release' version '2.6.0'
}

// https://github.com/researchgate/gradle-release
release {
    tagTemplate = '$name-$version'

    git {
        requireBranch = ''
    }
}

task printVersion {
    doLast {
        println "Released version " + project.property('version')
    }
}

afterReleaseBuild.dependsOn uploadArchives
afterReleaseBuild.dependsOn printVersion

def envs = ['bcslab', 'bcsprod', 'lab', 'prod']

envs.each { env ->
    def tar = task "build$env"(type: Tar) {
        compression = Compression.GZIP
        extension = 'tar.gz'
        classifier = env

        from env
    }

    build.dependsOn tar
}

artifacts {
    envs.each {
        archives tasks."build$it"
    }
}